plugins {
    id 'eclipse'
    id 'maven-publish'
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.1-SNAPSHOT" apply false
}

apply plugin: 'java'
apply plugin: 'architectury-plugin'
apply plugin: 'maven-publish'
apply plugin: 'dev.architectury.loom'

group = 'cx.rain.mc.catgirlnya'
archivesBaseName = 'catgirlnya'

version = '1.19.3-3.1.0'

if (System.getenv('GITHUB_SHA') != null) {
    version += "-${System.getenv('GITHUB_SHA').substring(0, 7)}"
} else if (System.getenv('JITPACK') == 'true') {
    version += "-${System.getenv('VERSION')}"
} else if (System.getenv("CI") == "true") {
    version += "-${System.getenv("CIRCLE_BRANCH")}"
    version += "-${System.getenv("CIRCLE_SHA1").substring(0, 7)}"
    version += "-${System.getenv("CIRCLE_BUILD_NUM")}"

    if (System.getenv("CIRCLE_PR_NUMBER") != null) {
        version += "-#${System.getenv("CIRCLE_PR_NUMBER")}"
    }
}

var release = false
if (release || (System.getenv('CAT_RELEASE') == 'true')) {
    version += '-release'
} else {
    version += '-dev'
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    silentMojangMappingsLicense()

    forge {

    }

    runs {
        data {
            data()
            programArgs '--all', '--mod', "catgirlnya",
                    '--output', file('src/generated/resources').absolutePath,
                    '--existing', file('src/main/resources').absolutePath
        }
    }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, " +
        "JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), " +
        "Arch: ${System.getProperty 'os.arch'}"

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

repositories {
    flatDir {
        dir 'libs'
    }

    maven {
        name = 'Jitpack'
        url = 'https://jitpack.io'
    }

    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    minecraft "com.mojang:minecraft:1.19.3"
    forge "net.minecraftforge:forge:1.19.3-44.1.23"

    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.19.3:2023.03.12@zip")
    }

    if (!release) {
        // Dev libs here.
    }
}

jar {
    archiveClassifier.set('')

    manifest {
        attributes([
                "Specification-Title"     : "catgirlnya",
                "Specification-Vendor"    : "qyl27",
                "Specification-Version"   : "3", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "qyl27",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
